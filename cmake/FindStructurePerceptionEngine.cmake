# - try to find Occipital Structure Core PerceptionEngine library
#
# Cache Variables: (probably not for direct use in your scripts)
#  STRUCTUREPERCEPTIONENGINE_INCLUDE_DIR
#  STRUCTUREPERCEPTIONENGINE_LIBRARY
#
# Non-cache variables you might use in your CMakeLists.txt:
#  STRUCTUREPERCEPTIONENGINE_FOUND
#  STRUCTUREPERCEPTIONENGINE_INCLUDE_DIRS
#  STRUCTUREPERCEPTIONENGINE_LIBRARIES
#  STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARIES - aka the dll for installing
#  STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY_DIRS
#
# Requires these CMake modules:
#  FindPackageHandleStandardArgs (known included with CMake >=2.6.2)
#
# Original Author:
# 2019 Russell Taylor: <russ@reliasolve.com>
# ReliaSolve
#
# Copyright Vality 2019.
# Distributed under the Boost Software License, Version 1.0.
# (See accompanying file LICENSE_1_0.txt or copy at
# http://www.boost.org/LICENSE_1_0.txt)

set(STRUCTUREPERCEPTIONENGINE_ROOT_DIR
	"${STRUCTUREPERCEPTIONENGINE_ROOT_DIR}"
	CACHE
	PATH
	"Directory to search for Structure PerceptionEngine")

if(CMAKE_SIZEOF_VOID_P MATCHES "8")
	set(_LIBSUFFIXES /lib64 /lib)
else()
	set(_LIBSUFFIXES /lib)
endif()

find_library(STRUCTUREPERCEPTIONENGINE_LIBRARY
	NAMES
	Structure
	PATHS
	"${STRUCTUREPERCEPTIONENGINE_ROOT_DIR}"
	PATH_SUFFIXES
	"${_LIBSUFFIXES}")

get_filename_component(_libdir "${STRUCTUREPERCEPTIONENGINE_LIBRARY}" PATH)

find_path(STRUCTUREPERCEPTIONENGINE_INCLUDE_DIR
	NAMES
	XRSesion.h
	HINTS
	"${_libdir}"
	"${_libdir}/.."
	PATHS
	"${STRUCTUREPERCEPTIONENGINE_ROOT_DIR}"
	PATH_SUFFIXES
	include/)

set(_deps_check)
if(WIN32)
	find_file(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY
		NAMES
		Structure.dll
		HINTS
		"${_libdir}"
		"${_libdir}/.."
		PATH_SUFFIXES
		bin)

	set(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARIES "${STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY}")
	get_filename_component(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY_DIRS
		"${STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY}"
		PATH)
	list(APPEND _deps_check STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY)
else()
	set(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY "${STRUCTUREPERCEPTIONENGINE_LIBRARY}")
	set(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARIES "${STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY}")
	get_filename_component(STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY_DIRS
		"${STRUCTUREPERCEPTIONENGINE_LIBRARY}"
		PATH)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(StructurePerceptionEngine
	DEFAULT_MSG
	STRUCTUREPERCEPTIONENGINE_LIBRARY
	STRUCTUREPERCEPTIONENGINE_INCLUDE_DIR
	${_deps_check})

if(STRUCTUREPERCEPTIONENGINE_FOUND)
	set(STRUCTUREPERCEPTIONENGINE_LIBRARIES "${STRUCTUREPERCEPTIONENGINE_LIBRARY}")
	set(STRUCTUREPERCEPTIONENGINE_INCLUDE_DIRS "${STRUCTUREPERCEPTIONENGINE_INCLUDE_DIR}")
	mark_as_advanced(STRUCTUREPERCEPTIONENGINE_ROOT_DIR)
endif()

mark_as_advanced(STRUCTUREPERCEPTIONENGINE_INCLUDE_DIR
	STRUCTUREPERCEPTIONENGINE_LIBRARY
	STRUCTUREPERCEPTIONENGINE_RUNTIME_LIBRARY)
